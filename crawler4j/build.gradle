

plugins {
    id  'java'
    id  'signing'
    id  'maven-publish'
    id  'com.github.johnrengelman.shadow'
    id  'cl.franciscosolis.sonatype-central-upload' version '1.0.3'
}

// description 'crawler4j: Open Source Web Crawler for Java'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

configurations.all {
    it.exclude group: 'commons-logging'
    it.exclude group: 'log4j'
    it.exclude group: 'org.apache.logging.log4j'
}

// https://stackoverflow.com/questions/23796404/could-not-find-method-compile-for-arguments-gradle

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '2.0.17'
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j', version: '2.0.17'
    implementation group: 'org.slf4j', name: 'jul-to-slf4j', version: '2.0.17'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.14'
    implementation group: 'org.apache.derby', name: 'derby', version: '10.17.1.0'
    implementation group: 'org.apache.derby', name: 'derbytools', version: '10.17.1.0'

    implementation group: 'org.apache.tika', name: 'tika-core', version: '2.9.4'
    implementation group: 'org.apache.tika', name: 'tika-parsers-standard-package', version: '2.9.4'
    implementation group: 'org.apache.tika', name: 'tika-langdetect-tika', version: '2.9.4'

    implementation group: 'io.github.pgalbraith', name: 'url-detector', version: '0.1.20'
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
    implementation group: 'de.malkusch.whois-server-list', name: 'public-suffix-list', version: '2.2.0'
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.5.18'

    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.wiremock', name: 'wiremock', version: '3.13.1'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.25'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.4-M5-groovy-3.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    // https://docs.gradle.org/current/userguide/signing_plugin.html
    def signingKey = System.getenv("GPG_SIGNING_KEY")
    def signingPassword = System.getenv("GPG_SIGNING_KEY_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign configurations.archives
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            from components.java

            artifact sourcesJar
            artifact javadocJar

            // Sign the pom
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        root.dependencies.removeAll { dep ->
            dep.scope == 'test'
        }

        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            name 'crawler4j'
            description 'crawler4j: Open Source Web Crawler for Java'
            url 'https://github.com/yasuhiroabe/crawler4j'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
            developers {
                developer {
                    id 'YasuhiroABE'
                    name 'Yasuhiro ABE'
		    email 'yasu@yasundial.org'
                }
            }
            scm {
                connection 'scm:git:git@github.com:YasuhiroABE/crawler4j.git'
                developerConnection 'scm:git:git@github.com:YasuhiroABE/crawler4j.git'
                url 'https://github.com/yasuhiroabe/crawler4j'
            }
        }
    }
}

// https://zenn.dev/orangain/articles/publish-to-maven-central-using-gradle
sonatypeCentralUpload {
    dependsOn("jar", "sourcesJar", "javadocJar", "generatePomFileForMavenJavaPublication")

    username = System.getenv("SONATYPE_USERNAME")
    password = System.getenv("SONATYPE_PASSWORD")

    archives = files(
        tasks.named("jar"),
        tasks.named("sourcesJar"),
        tasks.named("javadocJar"),
    )

    pom = file("${project.buildDir}/generated-pom.xml")

    signingKey = System.getenv("GPG_SIGNING_KEY")
    signingKeyPassphrase = System.getenv("GPG_SIGNING_KEY_PASSPHRASE")
}
